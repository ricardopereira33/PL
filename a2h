#!/usr/bin/gawk -f

BEGIN {
	FS = "[<>]";
	head = "<h1 align=\"center\"> Extracto ViaVerde </h1>\n<hr><h2> Cliente </h2>\n";
	head2 = "<h3> %s </h3> <p> %s </p>\n";
  	enc = "<html> <head> <meta charset='UTF-8'/> </head> <body>";
  	fmt = "<li><a> Dia %s: %d </a></li>\n\n";
  	entrada = "<li><a> %s </a></li>\n";
  	linha = "<p> %s </p>";
  	end = "</body> </html>";
  	elemento = "null";
}

/^<NOME|^<MORADA|^<LOCALIDADE|^<CODIGO_POSTAL/{
	dados[$2] = $3;
}

/^<DATA_ENTRADA/{
	if($3!="null"){
		split($3,a,"-");
		dias[a[1]]++;
		mes = a[2];
	}
	else{
		mes = "null";
	}
}

/^<DATA_SAIDA/ && mes=="null"{
	split($3,a,"-");
	mes = a[2];
}

/^<SAIDA/{
	locais[$3]++;
}

/^<IMPORTANCIA/{
	elemento = $3;
	sub(/,/,".",elemento)
	valor[mes] += elemento;
}

/^<TIPO>[Pp]arque/{
	parques[$3] += elemento;
}

END{
	print enc > "index.html";

	print head > "index.html";

	PROCINFO["sorted_in"]= "@ind_str_desc";
	for(i in dados){
		printf(head2,i,dados[i]) > "index.html";	
	}
	print "\n<hr>\n" > "index.html";

	PROCINFO["sorted_in"]= "@ind_str_asc";

	printf(head2,"Número de Entradas do Mês","") > "index.html";
  	for (i in dias) 
  		printf (fmt,i,dias[i]) > "index.html";
  	
  	printf(head2,"Lista de Locais de Saída","") > "index.html";
  	for(i in locais){
  		printf (entrada,i) > "index.html";
  	}

  	printf(head2,"Total Gasto","") > "index.html";
  	total = 0;
  	for(i in valor){
  		total += valor[i];
  		printf(linha,"Mês "i ": " valor[i] " €") > "index.html";
  	}
  	print "Valor Total : " total " €" > "index.html";

  	printf(head2,"Total Gasto em Parques","") > "index.html";
  	for(i in parques){
  		printf(linha,"Valor Total: " parques[i] " €") > "index.html";
  	}

  	print end > "index.html";
}